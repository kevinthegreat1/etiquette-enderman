plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"etiquetteenderman" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

publishMods {
	file = remapJar.archiveFile
	displayName = "Etiquette Enderman ${mod_version} for ${minecraft_version}"
	changelog = rootProject.file("CHANGELOG.md").text
	type = STABLE
	modLoaders.add("fabric")

	curseforge {
		projectId = "1383264"
		projectSlug = "etiquette-enderman" // Required for discord webhook
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		minecraftVersionRange {
			start = "1.21.10"
			end = "1.21.10"
		}
		requires("fabric-api")
	}
	modrinth {
		projectId = "30eYtjiG"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersionRange {
			start = "1.21.10"
			end = "1.21.10"
		}
	}
	github {
		repository = "kevinthegreat1/etiquette-enderman"
		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		commitish = "main"
	}
}
